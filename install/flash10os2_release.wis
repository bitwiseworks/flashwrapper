<WARPIN OS="OS2_4X">
	<HEAD>

	<REXX NAME="GetPathAndRenameOldFlash">
	  rc=rxFuncAdd('SysLoadFuncs','REXXUTIL','SysLoadFuncs')
	  rc=SysLoadFuncs()
	  parse upper arg request .

	  PluginPath=GetPluginPath()

	  if request = 'RENAME' then do
	   /* check and rename old Flash plugins in Mozilla dir */
	   call FlashRename PluginPath
	   /* check and rename old Flash plugins in plugins dir under MOZILLA_HOME*/
	   Browser=WirexxGetEnv('MOZILLA_HOME')'\plugins'
	   call FlashRename Browser
	   /* check and rename old Flash plugins in plugins dir */
	   Browser=WirexxGetEnv('MOZ_PLUGIN_PATH')
	   call FlashRename Browser
	   returncode = 0
	  end
	  else returncode = PluginPath

	  return PluginPath

	FlashRename: procedure
	  parse arg Brws
	  filetorenamefrom=Brws||'\npswf2.dll'
	  /* check for flash version */
	  flash7dll=Brws||'\flashwin.dll'
	  rc=sysfiletree(flash7dll,'flist','FT')
	  if flist.0=0 then
	  do
	    filetorenameto='npswf2.dll.flash5'
	    ren filetorenamefrom filetorenameto
	  end
	  else do
	    filetorenameto='npswf2.dll.flash7'
	    ren filetorenamefrom filetorenameto
	    filetorenamefrom=Brws||'\NPSWF32.dll'
	    filetorenameto='NPSWF32.dll.flash7'
	    ren filetorenamefrom filetorenameto
	    filetorenamefrom=Brws||'\flashwin.dll'
	    filetorenameto='flashwin.dll.flash7'
	    ren filetorenamefrom filetorenameto
	  end
	return

	GetPluginPath: procedure
	  PluginPath=WirexxGetEnv('MOZ_PLUGIN_PATH')
	  if PluginPath='' then do
	  Browser=GetBrowserPath()
	  PluginPath=Browser||'\plugins'
	  /* find possible .env setting file used for RUN!.EXE */
	  rc=sysfiletree(Browser'\*.env','flist','FO')
	  if flist.0 > 0 then
	  do
	  k = 0
	  do i = 1 to flist.0
	    rc = sysfilesearch('MOZ_PLUGIN_PATH', flist.i, 'result.')
	    do j = 1 to result.0
	     if pos('SET', translate(result.j)) = 1 then do
	      k = k + 1
	      parse var result.j . '=' PluginDir.k
	      end
	    end
	  if k > 0 then PluginPath = strip(PluginDir.k)
	  end /* check all .env files in browser dir */
	  end /* one or more .env files found */
	  end /* MOZ_PLUGIN_PATH env was empty */
	  return PluginPath

	GetBrowserPath: procedure
	  Browser=SysIni('USER','WPURLDEFAULTSETTINGS','DefaultBrowserExe')
	  if Browser='ERROR:' then do
	    Browser=SysIni('USER','Mozilla','Home')
	      if Browser='ERROR:' then do
	        rc=WirexxShowMessage('Browser lookup',,
	        'The Mozilla/Firefox browser could not be found!')
	        return 1
	      end
	  end
	  else do
	    exepos=lastpos('\',Browser)
	    if exepos>0 then do
	      Browser=left(Browser,exepos-1)
	    end
	    Browser=strip(Browser,'T','\')
	  end /* Browser dir was found */
	  return Browser

	</REXX>

	<REXX NAME="GetProgramPath">
	  rc=rxFuncAdd('SysLoadFuncs','REXXUTIL','SysLoadFuncs')
	  rc=SysLoadFuncs()

	  ProgramPath=WirexxGetEnv('PROGRAMS')
	  if ProgramPath='' then ProgramPath = "?:\PROGRAMS"
	  ProgramPath = strip(ProgramPath,'T','\')||"\FLASH10"
	  return ProgramPath
	
	</REXX>

	<VARPROMPT NAME=MOZILLA_HOME TYPE=PATH>
	The MOZILLA_HOME environment variable was not set up, please enter the path to your webbrowser homedirectory:
	</VARPROMPT>

	<TITLE>Flash 10 Plugin wrapper for eComStation Technology Preview Install</TITLE>
	
		<PCK INDEX=1
			PACKAGEID="Mensys BV\Flash 10\Odin DLLs\0\1\0"
			TITLE="Odin DLLs for Flash 10 plugin"
			TARGET="=("GetProgramPath")"
			NODESELECT
			CLEARPROFILE="USER\Flash10_Plugin\OdinPath"
			WRITEPROFILE="USER\Flash10_Plugin\OdinPath|$(1)"
		>The plugin is wrapper for Adobe Flash 9/10 Win32 plugin to Mozilla/FireFox/etc built for OS/2.
		It contains ODIN DLLs, adopted for the Flash plugin wrapper. There is no guarantee that this ODIN DLLs will be useful for our other WIN32 applications
		This ODIN dll's will be installed in separatedirectory and shouldn't be in stated in LIBPATH
		Plugin wrapper will search the dll's by FLASH10_ODIN environment variable or by keyword in Registry which will be added by this WPI package
		</PCK>

		<PCK INDEX=2
			PACKAGEID="Mensys BV\Flash 10\Plugin Wrapper\0\1\0"
			TITLE="Flash 10 plugin wrapper"
			TARGET="=("GetPathAndRenameOldFlash")"
			NODESELECT
			CLEARPROFILE="USER\Flash10_Plugin\PluginWrapperPath"
			WRITEPROFILE="USER\Flash10_Plugin\PluginWrapperPath|$(2)"
		>Flash 10 Plugin Wrapper
		</PCK>

		<PCK INDEX=3
			PACKAGEID="Mensys BV\Flash 10\Win32 plugin\0\1\0"
			TITLE="Adobe Flash 10 Win32 plugin"
			TARGET="=("GetPathAndRenameOldFlash")"
			NODESELECT
			CLEARPROFILE="USER\Flash10_Plugin\AdobePluginPath"
			WRITEPROFILE="USER\Flash10_Plugin\AdobePluginPath|$(3)"
		>Flash 10 Plugin Wrapper
		</PCK>

	</HEAD>
	<BODY>
		<PAGE INDEX=1 TYPE=README>
		<NEXTBUTTON TARGET=2>~Next</NEXTBUTTON>
		<TEXT>Flash 10 Plugin wrapper for eComStation readme</TEXT>
		<README EXTRACTFROMPCK="2" format=PLAIN>flash10.txt</README>
		</PAGE>

		<PAGE INDEX=2 TYPE=README>
		<NEXTBUTTON TARGET=3>~I Agree</NEXTBUTTON>
		<TEXT>ADOBE SYSTEMS INCORPORATED Warranty Disclaimer and Software License Agreement.</TEXT>
		<README EXTRACTFROMPCK="2" format=FLOW>flash10.lic</README>
		</PAGE>

		<PAGE INDEX=3 TYPE=TEXT>
		<NEXTBUTTON TARGET=4>~Next</NEXTBUTTON>
		<TEXT>
		Warning!!!

		All active Mozilla/Firefox browsers will have to be closed before continuing.

		Do you wish to continue?

		Select "Next" to continue.

		Select "Cancel" to abort installation.
		</TEXT>
		</PAGE>

		<PAGE INDEX=4 TYPE=CONTAINER>
		<NEXTBUTTON TARGET=5>~Next</NEXTBUTTON>
		<TEXT>Please select the packages which are to be installed. You may change the target paths for the packages.</TEXT>
		</PAGE>

		<PAGE INDEX=5 TYPE=TEXT>
		<NEXTBUTTON TARGET=0>I~nstall</NEXTBUTTON>
		<TEXT>Press "Install" to begin installing this archive.</TEXT>
		=("GetPathAndRenameOldFlash RENAME")
		</PAGE>
	</BODY>
</WARPIN>

